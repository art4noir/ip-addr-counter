plugins {
    id 'java'
}

group 'org.art4noir'
version '1.0'

repositories {
    mavenCentral()
}

sourceSets {
    jmh {
        java.srcDirs = ['src/main/java', 'src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.27'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.27'
}

test {
    minHeapSize = "1024m"
    maxHeapSize = "1024m"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.art4noir.addrcounter.App'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    main = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath

    // To enable the built-in stacktrace sampling profiler
    // args = ['-prof', 'stack']
}

// to make sure benchmarks always get compiled
classes.finalizedBy(jmhClasses)

